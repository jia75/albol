*> Copyright (C) 2025 D. Hargitt
*> This program has been made available under the GNU General Public License.
*> It is distributed WITHOUT ANY WARRANTY. See LICENSE.md for details.

IDENTIFICATION DIVISION.
PROGRAM-ID. ALBOL.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ALBUM-LIST-FILE ASSIGN TO "album-list.txt"
        ORGANIZATION IS SEQUENTIAL
        FILE STATUS IS FILE-STATUS-CODE.

DATA DIVISION.
FILE SECTION.
    FD ALBUM-LIST-FILE.
        01 ALBUM-RECORD.
            03 ALBUM-TITLE-RECORD PICTURE X(50).
            03 ALBUM-ARTIST-RECORD PICTURE X(50).
            03 ALBUM-RATING-RECORD PICTURE 9V99.
            03 ALBUM-RECORD-NEWLINE PICTURE X VALUE X"0A".

WORKING-STORAGE SECTION.
    01 CHOSEN-ACTION PICTURE X(4).
    01 ALBUM-TITLE PICTURE X(50).
    01 ALBUM-ARTIST PICTURE X(50).
    01 ALBUM-RATING PICTURE 9.99.
    01 FILE-STATUS-CODE PICTURE XX.
    01 EOF PICTURE X.
        88 EOF-REACHED VALUE 'Y'.
        88 EOF-NOT-REACHED VALUE 'N'.

PROCEDURE DIVISION.
MAIN-BRANCH.
    PERFORM LINE-SPLIT
    DISPLAY "Albol version 0, Copyright (C) 2025 D. Hargitt"
    DISPLAY "Albol comes with ABSOLUTELY NO WARRANTY; for details"
    DISPLAY "view LICENSE.md. This is free software, and you are"
    DISPLAY "welcome to redistribute it under certain conditions."
    PERFORM LINE-SPLIT
    DISPLAY "WELCOME TO ALBOL."
    DISPLAY "CHOOSE YOUR DESIRED FUNCTION FROM THE FOLLOWING."
    DISPLAY "ADD    ADD ALBUM RECORD."
    DISPLAY "VIEW   VIEW A INFORMATION ABOUT AN ALBUM RECORD."
    DISPLAY "EDIT   EDIT AN ALBUM RECORD."
    DISPLAY "MORE   MORE OPTIONS."
    DISPLAY "EXIT   EXIT THE APPLICATION."
    PERFORM LINE-SPLIT

    PERFORM UNTIL 1 EQUAL 0
        ACCEPT CHOSEN-ACTION

        PERFORM LINE-SPLIT

        EVALUATE CHOSEN-ACTION
            WHEN "ADD"
                PERFORM ADD-BRANCH
            WHEN "VIEW"
                CALL "VIEW-BRANCH"
            WHEN "EDIT"
                CALL "EDIT-BRANCH"
            WHEN "MORE"
                CALL "MORE-BRANCH"
            WHEN "EXIT"
                STOP RUN
            WHEN OTHER
                DISPLAY "FUNCTION NOT RECOGNIZED."
        END-EVALUATE

        PERFORM LINE-SPLIT
        DISPLAY "CHOOSE YOUR DESIRED FUNCTION FROM THE FOLLOWING."
        DISPLAY "ADD    ADD ALBUM RECORD."
        DISPLAY "VIEW   VIEW A INFORMATION ABOUT AN ALBUM RECORD."
        DISPLAY "EDIT   EDIT AN ALBUM RECORD."
        DISPLAY "MORE   MORE OPTIONS."
        DISPLAY "EXIT   EXIT THE APPLICATION."
        PERFORM LINE-SPLIT
    END-PERFORM

    STOP RUN.

ADD-BRANCH.
    DISPLAY "ADD ALBUM RECORD FUNCTION."
    PERFORM LINE-SPLIT
    DISPLAY "INPUT A TITLE."
    PERFORM LINE-SPLIT
    ACCEPT ALBUM-TITLE
    PERFORM LINE-SPLIT
    DISPLAY "INPUT AN ARTIST."
    PERFORM LINE-SPLIT
    ACCEPT ALBUM-ARTIST
    PERFORM LINE-SPLIT
    DISPLAY "INPUT A RATING."
    PERFORM LINE-SPLIT
    ACCEPT ALBUM-RATING

    PERFORM LINE-SPLIT

    OPEN INPUT ALBUM-LIST-FILE

    MOVE 'N' TO EOF
    PERFORM UNTIL EOF-REACHED
        READ ALBUM-LIST-FILE
            AT END MOVE 'Y' TO EOF
            NOT AT END IF ALBUM-TITLE-RECORD EQUAL ALBUM-TITLE
                DISPLAY "RECORD ALREADY EXISTS."
                CLOSE ALBUM-LIST-FILE
                EXIT PARAGRAPH
            END-IF
    END-PERFORM

    CLOSE ALBUM-LIST-FILE

    OPEN I-O ALBUM-LIST-FILE

    MOVE 'N' TO EOF
    PERFORM UNTIL EOF-REACHED
        READ ALBUM-LIST-FILE
            AT END MOVE 'Y' TO EOF
            NOT AT END IF ALBUM-TITLE-RECORD EQUAL " "
                MOVE ALBUM-TITLE TO ALBUM-TITLE-RECORD
                MOVE ALBUM-ARTIST TO ALBUM-ARTIST-RECORD
                MOVE ALBUM-RATING TO ALBUM-RATING-RECORD
                REWRITE ALBUM-RECORD
                CLOSE ALBUM-LIST-FILE
                DISPLAY "SUCCESS."
                EXIT PARAGRAPH
            END-IF
    END-PERFORM

    CLOSE ALBUM-LIST-FILE

    INITIALIZE ALBUM-RECORD
    MOVE ALBUM-TITLE TO ALBUM-TITLE-RECORD
    MOVE ALBUM-ARTIST TO ALBUM-ARTIST-RECORD
    MOVE ALBUM-RATING TO ALBUM-RATING-RECORD
    MOVE X"0A" TO ALBUM-RECORD-NEWLINE

    OPEN EXTEND ALBUM-LIST-FILE

    IF FILE-STATUS-CODE EQUAL "35"
        PERFORM LINE-SPLIT
        DISPLAY "ERROR: FILE 'album-list.txt' DOES NOT EXIST."
        DISPLAY "PLEASE CREATE THE FILE."
        CLOSE ALBUM-LIST-FILE
        EXIT PARAGRAPH
    ELSE
        IF FILE-STATUS-CODE NOT EQUAL "00"
            PERFORM LINE-SPLIT
            DISPLAY "WRITE FAILED WITH STATUS: " FILE-STATUS-CODE
            CLOSE ALBUM-LIST-FILE
            EXIT PARAGRAPH
        END-IF
    END-IF

    WRITE ALBUM-RECORD

    DISPLAY "SUCCESS."

    CLOSE ALBUM-LIST-FILE.

LINE-SPLIT.
    DISPLAY "====================================================".
