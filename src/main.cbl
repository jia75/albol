>>SOURCE FORMAT FREE
*> Copyright (C) 2025 D. Hargitt
*> This program has been made available under the GNU General Public License.
*> It is distributed WITHOUT ANY WARRANTY. See LICENSE.md for details.

IDENTIFICATION DIVISION.
PROGRAM-ID. MAIN.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ALBUM-LIST-FILE ASSIGN TO "album-list.txt"
        ORGANIZATION IS SEQUENTIAL
        FILE STATUS IS FILE-STATUS-CODE.
    SELECT TRACK-LIST-FILE ASSIGN TO "track-list.txt"
        ORGANIZATION IS SEQUENTIAL
        FILE STATUS IS FILE-STATUS-CODE-TRACK.

DATA DIVISION.
FILE SECTION.
    FD ALBUM-LIST-FILE.
        01 ALBUM-RECORD.
            03 ALBUM-TITLE-RECORD PICTURE X(50).
            03 ALBUM-ARTIST-RECORD PICTURE X(50).
            03 ALBUM-RATING-RECORD PICTURE 9V99.
            03 ALBUM-RECORD-NEWLINE PICTURE X VALUE X"0A".
    FD TRACK-LIST-FILE.
        01 TRACK-RECORD.
            03 TRACK-NUMBER-RECORD PICTURE 99.
            03 TRACK-TITLE-RECORD PICTURE X(50).
            03 TRACK-ALBUM-RECORD PICTURE X(50).
            *> 03 TRACK-RATING-RECORD PICTURE 9V99.
            03 TRACK-RECORD-NEWLINE PICTURE X VALUE X"0A".

WORKING-STORAGE SECTION.
    01 CHOSEN-ACTION PICTURE X(4).
    01 FILE-STATUS-CODE PICTURE XX.
    01 FILE-STATUS-CODE-TRACK PICTURE XX.

PROCEDURE DIVISION.
MAIN-BRANCH.
    PERFORM LINE-SPLIT
    DISPLAY "Albol version 1.1.0, Copyright (C) 2025 D. Hargitt"
    DISPLAY "Albol comes with ABSOLUTELY NO WARRANTY; for details"
    DISPLAY "view LICENSE.md. This is free software, and you are"
    DISPLAY "welcome to redistribute it under certain conditions."
    PERFORM LINE-SPLIT
    DISPLAY "WELCOME TO ALBOL."
    PERFORM LINE-SPLIT

    PERFORM UNTIL 1 EQUAL 0
        DISPLAY "CHOOSE YOUR DESIRED FUNCTION FROM THE FOLLOWING."
        DISPLAY "ADD    ADD ALBUM RECORD."
        DISPLAY "VIEW   VIEW A INFORMATION ABOUT AN ALBUM RECORD."
        DISPLAY "EDIT   EDIT AN ALBUM RECORD."
        DISPLAY "TRACK  ACCESS INDIVIDUAL TRACK OPTIONS."
        DISPLAY "MORE   MORE OPTIONS."
        DISPLAY "EXIT   EXIT THE APPLICATION."
        PERFORM LINE-SPLIT

        OPEN INPUT ALBUM-LIST-FILE
        IF FILE-STATUS-CODE EQUAL "35"
            DISPLAY "ERROR: FILE 'album-list.txt' DOES NOT EXIST."
            DISPLAY "PLEASE CREATE THE FILE VIA MORE > INIT."
            PERFORM LINE-SPLIT
        ELSE
            IF FILE-STATUS-CODE NOT EQUAL "00"
                DISPLAY "FILE FAILED WITH STATUS: " FILE-STATUS-CODE
                PERFORM LINE-SPLIT
            END-IF
        END-IF
        CLOSE ALBUM-LIST-FILE

        OPEN INPUT TRACK-LIST-FILE
        IF FILE-STATUS-CODE-TRACK EQUAL "35"
            DISPLAY "ERROR: FILE 'track-list.txt' DOES NOT EXIST."
            DISPLAY "PLEASE CREATE THE FILE VIA MORE > INIT."
            PERFORM LINE-SPLIT
        ELSE
            IF FILE-STATUS-CODE-TRACK NOT EQUAL "00"
                DISPLAY "FILE FAILED WITH STATUS: " FILE-STATUS-CODE-TRACK
                PERFORM LINE-SPLIT
            END-IF
        END-IF
        CLOSE TRACK-LIST-FILE
        
        ACCEPT CHOSEN-ACTION
        PERFORM LINE-SPLIT

        MOVE FUNCTION UPPER-CASE(CHOSEN-ACTION) TO CHOSEN-ACTION

        EVALUATE CHOSEN-ACTION
            WHEN "ADD"
                CALL "ADD-BRANCH"
            WHEN "VIEW"
                CALL "VIEW-BRANCH"
            WHEN "EDIT"
                CALL "EDIT-BRANCH"
            WHEN "MORE"
                CALL "MORE-BRANCH"
            WHEN "TRAC"
                CALL "TRAC-BRANCH"
            WHEN "EXIT"
                STOP RUN
            WHEN OTHER
                DISPLAY "FUNCTION NOT RECOGNIZED."
                PERFORM LINE-SPLIT
        END-EVALUATE
    END-PERFORM

    STOP RUN.

LINE-SPLIT.
    DISPLAY "====================================================".
